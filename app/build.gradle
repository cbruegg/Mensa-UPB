apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-allopen'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"

    compileOptions.incremental = true

    defaultConfig {
        applicationId "com.cbruegg.mensaupb"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 13
        versionName "1.4.5"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        def apiIdProps = new Properties()
        apiIdProps.load(new FileInputStream(rootProject.file("api_id.properties")))
        buildConfigField "String", "API_ID", "\"" + apiIdProps.id + "\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'

        androidTest {
            allOpen {
                annotation("com.cbruegg.mensaupb.util.AllOpen")
            }
        }
    }

    dataBinding {
        enabled = true
    }
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    def supportVersion = "26.0.1"
    compile "com.android.support:appcompat-v7:$supportVersion"
    compile "com.android.support:recyclerview-v7:$supportVersion"
    compile "com.android.support:design:$supportVersion"
    compile "com.android.support:preference-v7:$supportVersion"
    compile "com.android.support:customtabs:$supportVersion"
    compile "com.android.support:support-annotations:$supportVersion"
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    compile 'com.squareup.okhttp3:okhttp:3.8.1'

    def moshiVersion = "1.5.0"
    compile "com.squareup.moshi:moshi-parent:$moshiVersion"
    compile "com.squareup.moshi:moshi-adapters:$moshiVersion"
    compile "com.squareup.moshi:moshi-kotlin:$moshiVersion"
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.jakewharton:kotterknife:0.1.0-SNAPSHOT'

    def funktionaleVersion = "1.1"
    compile "org.funktionale:funktionale:$funktionaleVersion"
    compile "org.funktionale:funktionale-either:$funktionaleVersion"

    kapt "com.android.databinding:compiler:$rootProject.androidToolsVersion"

    def daggerVersion = "2.11"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    compile "com.google.dagger:dagger:$daggerVersion"
    provided 'javax.annotation:jsr250-api:1.0'

    def coroutineVersion = "0.17"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutineVersion"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutineVersion"

    def requeryVersion = "1.4.0"
    compile "io.requery:requery:$requeryVersion"
    compile "io.requery:requery-android:$requeryVersion"
    compile "io.requery:requery-kotlin:$requeryVersion"
    kapt "io.requery:requery-processor:$requeryVersion"

    compile "org.ccil.cowan.tagsoup:tagsoup:1.2.1"

    def stethoVersion = "1.5.0"
    debugCompile "com.facebook.stetho:stetho:$stethoVersion"
    debugCompile "com.facebook.stetho:stetho-okhttp3:$stethoVersion"

    def aarchVersion = "1.0.0-alpha7"
    compile "android.arch.lifecycle:runtime:$aarchVersion"
    compile "android.arch.lifecycle:extensions:$aarchVersion"
    kapt "android.arch.lifecycle:compiler:$aarchVersion"

    androidTestCompile "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
    androidTestCompile "junit:junit:4.12"
    androidTestCompile 'com.android.support.test:runner:1.0.0'
    androidTestCompile 'com.android.support.test:rules:1.0.0'
    androidTestCompile "com.android.support:support-annotations:$supportVersion"
    androidTestCompile "org.mockito:mockito-android:2.8.47"
}

buildscript {
    ext.kotlinVersion = '1.1.4-2'
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlinVersion"
    }
}
repositories {
    google()
    jcenter()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}
