apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    compileOptions.incremental = true

    defaultConfig {
        applicationId "com.cbruegg.mensaupb"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 12
        versionName "1.4.4"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        def apiIdProps = new Properties()
        apiIdProps.load(new FileInputStream(rootProject.file("api_id.properties")))
        buildConfigField "String", "API_ID", "\"" + apiIdProps.id + "\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    dataBinding {
        enabled = true
    }
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    def supportVersion = "25.3.0"
    compile "com.android.support:appcompat-v7:$supportVersion"
    compile "com.android.support:recyclerview-v7:$supportVersion"
    compile "com.android.support:design:$supportVersion"
    compile "com.android.support:preference-v7:$supportVersion"
    compile "com.android.support:customtabs:$supportVersion"
    compile "com.android.support:support-annotations:$supportVersion"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    compile 'com.squareup.okhttp3:okhttp:3.6.0'

    compile 'com.github.cbruegg.moshi:moshi-parent:8f7d17f9c61199ec83d3f4927161de2ca5f5ade1'
    compile 'com.github.cbruegg.moshi:moshi-adapters:8f7d17f9c61199ec83d3f4927161de2ca5f5ade1'
    compile 'com.cbruegg.sikoan-mvp:sikoan-mvp:1.0.2'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.jakewharton:kotterknife:0.1.0-SNAPSHOT'
    compile 'org.funktionale:funktionale:1.0.0-final'
    compile 'org.funktionale:funktionale-either:1.0.0-final'

    kapt "com.android.databinding:compiler:$rootProject.androidToolsVersion"

    kapt 'com.google.dagger:dagger-compiler:2.9'
    compile 'com.google.dagger:dagger:2.9'
    provided 'javax.annotation:jsr250-api:1.0'

    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.14'
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-android:0.14'

    def requeryVersion = "1.2.1"
    compile "io.requery:requery:$requeryVersion"
    compile "io.requery:requery-android:$requeryVersion"
    compile "io.requery:requery-kotlin:$requeryVersion"
    kapt "io.requery:requery-processor:$requeryVersion"

    def stethoVersion = "1.4.2"
    debugCompile "com.facebook.stetho:stetho:$stethoVersion"
    debugCompile "com.facebook.stetho:stetho-okhttp3:$stethoVersion"

    androidTestCompile "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
    androidTestCompile "junit:junit:4.12"
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
    androidTestCompile "com.android.support:support-annotations:$supportVersion"
}

buildscript {
    ext.kotlinVersion = '1.1.1'
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}
repositories {
    jcenter()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
}
